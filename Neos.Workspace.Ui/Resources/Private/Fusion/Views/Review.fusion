Neos.Workspace.Ui.WorkspaceController.review = Neos.Fusion:Component {
  ///todo  Neos\ContentRepository\Core\Projection\Workspace\Workspace
  selectedWorkspaceName = ${selectedWorkspaceName}
  /// string
  selectedWorkspaceLabel = ${selectedWorkspaceLabel}

  baseWorkspaceName = ${baseWorkspaceName}

  baseWorkspaceLabel = ${baseWorkspaceLabel}
  /// bool
  canPublishToBaseWorkspace = ${canPublishToBaseWorkspace}
  /// bool
  canPublishToWorkspace = ${canPublishToWorkspace}
  /// array
  siteChanges = ${siteChanges}
  /// array
  contentDimensions = ${contentDimensions}
  /// array
  flashMessages = ${flashMessages}

  i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}

  renderer = Neos.Fusion:Match {
    @subject = ${request.format}
    @default = afx`
      <section
        hx-indicator="#indicator"
        hx-boost="true"
        id='workspaceReview'
      >
        <Neos.Workspace.Ui:Component.HTMXConfig/>
        <Neos.Workspace.Ui:Component.FlashMessages flashMessages={props.flashMessages}/>
        <Neos.Workspace.Ui:Component.Indicator/>
        <main class="neos-content neos-container-fluid">
          <div class="neos-row-fluid">
            <legend>
              {props.i18n.id('workspaces.unpublishedChanges').arguments([selectedWorkspaceLabel]).translate()}
            </legend>

            <Neos.Fusion.Form:Form
              form.target.action="publishOrDiscardNodes"
              form.target.format="htmx"
              form.target.arguments.workspaceName={props.selectedWorkspaceName}
              attributes.class="neos-inline"
              attributes.hx-post={form.getTarget()}
              attributes.id="publishOrDiscardNodesForm"
            >
            <Neos.Workspace.Ui:Component.ReviewTable
              @if.hasSiteChanges={props.siteChanges}
              siteChanges={props.siteChanges}
              selectedWorkspaceName={props.selectedWorkspaceName}
              selectedWorkspaceLabel={props.selectedWorkspaceLabel}
              canPublishToBaseWorkspace={props.canPublishToBaseWorkspace}
              canPublishToWorkspace={props.canPublishToWorkspace}/>
            <div @if.hasNoSiteChange={!props.siteChanges} class="no-unpublished-changes">
              {props.i18n.id('workspaces.reviewWorkspace.disabled').arguments([selectedWorkspaceLabel]).translate()}
            </div>
            <Neos.Workspace.Ui:Component.ReviewActions
              hasSiteChanges={!!props.siteChanges}
              canPublishToBaseWorkspace={props.canPublishToBaseWorkspace}
              canPublishToWorkspace={props.canPublishToWorkspace}
              selectedWorkspaceName={props.selectedWorkspaceName}
              baseWorkspaceLabel={props.baseWorkspaceLabel}
            />
            </Neos.Fusion.Form:Form>
          </div>
        </main>
      </section>
      <div id="popover-container"></div>
    `
    htmx = afx`
      <Neos.Workspace.Ui:Component.FlashMessages flashMessages={props.flashMessages}/>
      <Neos.Workspace.Ui:Component.ReviewTable
        @if.hasSiteChanges={props.siteChanges}
        siteChanges={props.siteChanges} selectedWorkspaceName={selectedWorkspaceName} selectedWorkspaceLabel={selectedWorkspaceLabel} />
    `
  }
}
