prototype(Neos.Workspace.Ui:Component.Modal.ConfirmPublishAllChanges) < prototype(Neos.Fusion:Component) {
  workspaceTitle = ''


  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'confirm-publish-all-changes-popover'

    publishWorkspaceUri = Neos.Fusion:ActionUri {
      action = 'publishWorkspace'
      format = 'htmx'
      arguments {
        workspace = ${workspaceName}
      }
    }
  }

  renderer = afx`
    <div popover id={private.popoverId} hx-indicator="#indicator">
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.publishAllChangesInWorkspaceConfirmation').arguments([props.workspaceTitle])}
        </div>
      </header>
      <section>
        <Neos.Workspace.Ui:Component.Button
          label={"cancel"}
          attributes.popovertarget={private.popoverId}
          attributes.popovertargetaction="close"
        />
        <Neos.Workspace.Ui:Component.Button
          isSuccess
          label={private.i18n.id('workspaces.publishAllChanges')}
          icon="fas fa-check-double icon-white"
          attributes.hx-target='body'
          attributes.hx-swap='innerHTML'
          attributes.hx-get={private.publishWorkspaceUri}
        />
      </section>
    </div>
  `
}
