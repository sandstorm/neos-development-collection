prototype(Neos.Workspace.Ui:Component.Modal.Edit) < prototype(Neos.Fusion:Component) {
  workspaceName = ''
  workspaceTitle = ''
  workspaceDescription = ''
  baseWorkspaceName = ''

  workspaceHasChanges = false
  /// array<workspaceName: string, workspaceTitle: string>
  baseWorkspaceOptions = ${[]}

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'workspace-edit-modal'
    workspaceTableRowId = ${'workspace-row-' + props.workspaceName}
  }

  prototype(Neos.Fusion.Form:LabelRenderer) {
    translationPackage = 'Neos.Workspace.Ui'
    translationSource = 'Main'
  }

  prototype(Neos.Fusion.Form:Neos.BackendModule.FieldContainer) {
    translation.label {
      package = 'Neos.Workspace.Ui'
      source = 'Main'
    }
  }

  renderer = afx`
    <div popover id={private.popoverId}>
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.editWorkspace').arguments([props.workspaceTitle])}
        </div>
      </header>
      <section>
        <Neos.Fusion.Form:Form
          form.target.action="update"
          form.target.format="htmx"
          form.target.arguments.workspaceName={props.workspaceName}
          attributes.class="neos-inline"
          attributes.hx-post={form.getTarget()}
          attributes.hx-disabled-elt="find button"
          attributes.hx-on--after-request={'document.getElementById("' + private.popoverId + '").hidePopover()'}
        >
          <fieldset>
            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="title"
              field.value={props.workspaceTitle}
              label="workspaces.workspace.title"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Textfield
                attributes.required
                attributes.pattern="/^[\p{L}\p{P}\d \.]{1,200}$/u"
                attributes.autofocus
                attributes.placeholder={props.workspaceName}
              />
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="description"
              field.value={props.workspaceDescription}
              label="workspaces.workspace.description"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Textfield
                attributes.pattern="/^[\p{L}\p{P}\d \.]{0,500}$/u"
              />
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="baseWorkspace"
              field.value={props.baseWorkspaceName}
              label="workspaces.workspace.baseWorkspace"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Select
                attributes.disabled={props.workspaceHasChanges}
              >
                <Neos.Fusion:Loop items={props.baseWorkspaceOptions} itemName="workspaceTitle" itemKey="workspaceName">
                  <Neos.Fusion.Form:Select.Option
                    option.value={workspaceName}
                    attributes.selected={workspaceName == props.baseWorkspaceName}
                  >
                    {workspaceTitle}
                  </Neos.Fusion.Form:Select.Option>
                </Neos.Fusion:Loop>
              </Neos.Fusion.Form:Select>
              <!-- TODO: use component and styling -->
              <p
                style={{ marginTop: '.5em' }}
                @if.workspaceHasChanges={props.workspaceHasChanges}
              >
                <i
                  class="fas fa-exclamation-triangle"
                  style={{ color: 'var(--warningText)', marginRight: '.5em' }}
                ></i>{' '}
                {private.i18n.id('workspaces.cantChangeBaseWorkspaceBecauseWorkspaceContainsChanges')}
              </p>
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>
          </fieldset>
          <footer>
            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">
              {private.i18n.id('applyChanges')}
            </Neos.Fusion.Form:Button>
            <button
              type="button"
              class="neos-button"
              popovertarget={private.popoverId}
              popovertargetaction="close"
            >
              {private.i18n.id('cancel', 'Cancel', [])}
            </button>
          </footer>
        </Neos.Fusion.Form:Form>
      </section>
    </div>
  `
}
