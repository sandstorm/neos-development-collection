##
# Renders a document for review
#
prototype(Neos.Workspace.Ui:Component.ReviewDocumentTableRow) < prototype(Neos.Fusion:Component) {

  document = null
  documentPath = ''
  selectedWorkspaceName = ${selectedWorkspaceName}
  selectedWorkspaceLabel = ${selectedWorkspaceLabel}
  canPublishToBaseWorkspace = ${canPublishToBaseWorkspace}
  canPublishToWorkspace = ${canPublishToWorkspace}

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}

    openNodeInWorkspaceAction = Neos.Fusion:ActionUri {
      action = 'rebaseAndRedirect'
      arguments {
        targetWorkspaceName = ${selectedWorkspaceName}
        targetNode = ${document.document.documentNodeAddress}

      }
    }
    openNodeInPreviewAction = Neos.Fusion:ActionUri {
      request=${request.mainRequest}
      package='Neos.Neos'
      controller='Frontend\\\Node'
      action = 'preview'
      // todo fixme
      @process.addQuery = ${value + '?node=' + document.document.documentNodeAddress}

    }
    discardDocumentAction = Neos.Fusion:ActionUri {
      action = 'discardDocument'
      format = 'json'
      arguments {
        nodeAddress = ${document.document.documentNodeAddress}
        selectedWorkspace = ${selectedWorkspaceName}
      }
    }
    publishDocumentAction = Neos.Fusion:ActionUri {
      action = 'publishDocument'
      format = 'json'
      arguments {
        nodeAddress = ${document.document.documentNodeAddress}
        selectedWorkspace = ${selectedWorkspaceName}
      }
    }

  }

  renderer = afx`
    <tr>
      <Neos.Fusion:DataStructure
        @path="attributes.class"
        general="neos-document"
        deleted={document.documentChanges.isRemoved ? 'legend-deleted' : ''}
        created={document.documentChanges.isNew ? 'legend-created' : ''}
        moved={document.documentChanges.isMoved ? 'legend-moved' : ''}
      />
      <td>
        <div class="toggle-document neos-button">
          <i class="fas icon-white fa-chevron-down"></i>
        </div>
      </td>
      <td>
        <div class="neos-button">
          <label for={"check-document-" + document.document.aggregateId} class="neos-checkbox">
            <input type="checkbox" class="check-document" id={"check-document-" + document.document.aggregateId} /><span></span>
          </label>
        </div>
      </td>
      <td class="document-details">
        <Neos.Fusion:Loop items={document.document.documentBreadCrumb} itemName="crumb">
          <Neos.Workspace.Ui:Component.Icon icon="fas fa-caret-right" @if.notFirst={!iterator.isFirst} />
          <span>
            <span class="node-icons" @if.hasIcon={document.document.documentIcon} @if.isLast={iterator.isLast}>
                <Neos.Workspace.Ui:Component.Icon icon={document.document.documentIcon} />
                <i @if.nodeIsHidden={document.documentChanges.isHidden} class="icon-red fa-solid fa-circle-xmark"></i>
            </span>
            <span>
              {crumb}
            </span>
          </span>
        </Neos.Fusion:Loop>
      </td>
      <td class="document-actions">
            <!-- TODO: Does currently not work because of broken change projection -->
        <Neos.Workspace.Ui:Component.Button
                title={props.canPublishToWorkspace ? private.i18n.id('workspaces.discardAllChangesInThisDocument'): private.i18n.id('workspaces.changes.noPermissionToPublishToWorkspace')}
                icon="fas fa-trash-alt icon-white"
                isDanger={true}
                attributes.hx-get={private.discardDocumentAction}
                attributes.hx-swap='innerHTML'
                attributes.hx-target="#workspaceReview"
                attributes.hx-swap="innerHTML"
                attributes.disabled={!props.canPublishToWorkspace}
        />
        <Neos.Workspace.Ui:Component.Button
                title={props.canPublishToBaseWorkspace ? private.i18n.id('workspaces.publishAllChangesInThisDocument') :  private.i18n.id('workspaces.changes.noPermissionToPublishToBaseWorkspace')}
                icon="fas fa-check icon-white"
                isSuccess={true}
                attributes.hx-get={private.publishDocumentAction}
                attributes.hx-swap='innerHTML'
                attributes.hx-target="#workspaceReview"
                attributes.hx-swap="innerHTML"
                attributes.disabled={!props.canPublishToBaseWorkspace}
        />

        <a href={private.openNodeInWorkspaceAction}
           target="_blank"
           title={private.i18n.id('edit')}
           class="neos-button neos-button-primary"
           disabled={document.documentChanges.isRemoved}>
          <Neos.Workspace.Ui:Component.Icon icon="fas fa-pencil-alt" />
        </a>
        <a href={private.openNodeInPreviewAction}
           target="_blank"
           title={private.i18n.id('workspaces.openPageInWorkspace').arguments([selectedWorkspaceLabel])}
           class="neos-button neos-button-primary">
          <Neos.Workspace.Ui:Component.Icon icon="fas fa-external-link-alt" />
        </a>

      </td>

    </tr>
  `
}
