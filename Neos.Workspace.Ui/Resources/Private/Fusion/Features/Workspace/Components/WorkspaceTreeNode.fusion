##
# Renders a single workspace list item and its subworkspaces
#
prototype(Neos.Workspace.Ui:Component.WorkspaceTreeNode) < prototype(Neos.Fusion:Component) {
  /// Neos\Workspace\Ui\ViewModel\WorkspaceListItem|null
  workspaceListItem = null
  /// Neos\Workspace\Ui\ViewModel\WorkspaceListItems
  workspaceListItems = null
  /// int
  level = 0

  @private {
    baseWorkspaceListItems = ${Array.filter(props.workspaceListItems, (workspaceListItem) => workspaceListItem.baseWorkspaceName == (props.workspaceListItem.name || 'live'))}
  }

  renderer = afx`
    <Neos.Workspace.Ui:Component.WorkspaceTableRow
      workspaceListItem={props.workspaceListItem}
      level={props.level}
      @if={props.workspaceListItem != null}
    />
    <Neos.Fusion:Loop items={private.baseWorkspaceListItems} itemName="workspaceListItem">
      <Neos.Workspace.Ui:Component.WorkspaceTreeNode
        workspaceListItem={workspaceListItem}
        workspaceListItems={props.workspaceListItems}
        level={props.level + 1}
      />
    </Neos.Fusion:Loop>
  `
}
