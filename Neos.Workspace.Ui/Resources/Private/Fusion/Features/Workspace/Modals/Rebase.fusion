prototype(Neos.Workspace.Ui:Component.Modal.Rebase) < prototype(Neos.Fusion:Component) {
  /// string
  workspaceName = null

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'workspace-rebase-modal'

    forceRebaseWorkspaceUri = Neos.Fusion:ActionUri {
      action = 'rebase'
      format = 'htmx'
      arguments {
        workspaceName = ${workspaceName}
        force = true
      }
    }
  }

  renderer = afx`
    <div popover id={private.popoverId}>
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.dialog.forceSyncWorkspace')}
        </div>
      </header>
      <section>
        <p>{private.i18n.id('workspaces.dialog.thisCanLeadToLostChanges')}</p>
      </section>
      <footer>
        <button
          type="button"
          class="neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          {private.i18n.id('cancel')}
        </button>
        <Neos.Workspace.Ui:Component.Button
          isDanger
          label={private.i18n.id('workspaces.modal.yesRebaseWorkspace')}
          attributes.hx-get={private.forceRebaseWorkspaceUri}
          attributes.hx-target="#workspace-module-content"
          attributes.hx-swap="outerHTML"
          attributes.hx-on--after-request={'document.getElementById("' + private.popoverId + '").hidePopover()'}
        />
      </footer>
    </div>
  `
}
