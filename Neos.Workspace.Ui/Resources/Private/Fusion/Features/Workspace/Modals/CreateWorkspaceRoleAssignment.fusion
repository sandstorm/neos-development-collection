prototype(Neos.Workspace.Ui:Component.Modal.CreateWorkspaceRoleAssignment) < prototype(Neos.Fusion:Component) {
  workspaceName=''
  workspaceTitle=''

  userOptions=${[]}
  groupOptions=${[]}
  subjectTypeOptions=${[]}
  roleOptions=${[]}

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'create-workspace-role-assignment-popover'

    // Default SubjectType is USER.
    defaultSubjectType = 'USER'
  }

  prototype(Neos.Fusion.Form:LabelRenderer) {
    translationPackage = 'Neos.Workspace.Ui'
    translationSource = 'Main'
  }

  prototype(Neos.Fusion.Form:Neos.BackendModule.FieldContainer) {
    translation.label {
      package = 'Neos.Workspace.Ui'
      source = 'Main'
    }
  }

  // TODO: hide/show user/group select based on subjectType

  renderer = afx`
    <div popover id={private.popoverId}>
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.createWorkspaceRoleAssignment').arguments([props.workspaceTitle])}
        </div>
      </header>
      <section>
        <Neos.Fusion.Form:Form
          form.target.action="addWorkspaceRoleAssignment"
          form.target.format="htmx"
          form.target.arguments.workspaceName={props.workspaceName}
          attributes.class="neos-inline"
          attributes.hx-post={form.getTarget()}
        >
          <fieldset>
            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="subjectType"
              field.value={private.defaultSubjectType}
              label="workspaces.workspace.workspaceRoleAssignment.subjectType"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Select>
                <Neos.Fusion:Loop items={props.subjectTypeOptions} itemName="subjectTypeTitle"
                                  itemKey="subjectTypeValue">
                  <Neos.Fusion.Form:Select.Option
                    option.value={subjectTypeValue}
                    attributes.selected={subjectTypeValue == private.defaultSubjectType}
                  >
                    {subjectTypeTitle}
                  </Neos.Fusion.Form:Select.Option>
                </Neos.Fusion:Loop>
              </Neos.Fusion.Form:Select>
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="subject"
              field.value=""
              label="workspaces.workspace.workspaceRoleAssignment.subject"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Select>
                <option value=""></option>
                <Neos.Fusion:Loop items={props.userOptions} itemName="userName" itemKey="userId">
                  <Neos.Fusion.Form:Select.Option
                    option.value={userId}
                  >
                    {userName}
                  </Neos.Fusion.Form:Select.Option>
                </Neos.Fusion:Loop>
              </Neos.Fusion.Form:Select>
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer
              field.name="role"
              label="workspaces.workspace.workspaceRoleAssignment.role"
            >
              <Neos.Fusion.Form:Select>
                <Neos.Fusion:Loop items={props.roleOptions} itemName="roleName" itemKey="roleIdentifier">
                  <Neos.Fusion.Form:Select.Option
                    option.value={roleIdentifier}
                  >
                    {roleName}
                  </Neos.Fusion.Form:Select.Option>
                </Neos.Fusion:Loop>
              </Neos.Fusion.Form:Select>
            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>
          </fieldset>
          <footer>
            <Neos.Workspace.Ui:Component.Button
              title={'add'}
              icon="plus"
              attributes.hx-get={form.getTarget()}
              attributes.hx-target={'#' + private.popoverId}
              attributes.hx-swap='outerHTML'
              attributes.hx-on--after-request={'document.getElementById("' + private.popoverId + '").showPopover()'}
            />
          </footer>
        </Neos.Fusion.Form:Form>
      </section>
    </div>
  `
}
