prototype(Neos.Workspace.Ui:Component.Modal.ConfirmDiscardSelectedChanges) < prototype(Neos.Fusion:Component) {
  workspaceTitle=''

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'confirm-discard-selected-changes-popover'

    confirmDiscardSelectedChanges = Neos.Fusion:ActionUri {
      action = 'publishOrDiscardNodes'
      format = 'htmx'
      arguments {
        workspace = ${workspaceName}
        action = 'discard'
      }
    }
  }

  renderer = afx`
    <div popover id={private.popoverId} hx-indicator="#indicator">
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.discardSelectedChanges')}
        </div>
      </header>
      <section>
        <Neos.Workspace.Ui:Component.Button
          label={private.i18n.id('cancel')}
          attributes.popovertarget={private.popoverId}
          attributes.popovertargetaction="close"
        />
        <div class="neos-pull-right">
          <Neos.Workspace.Ui:Component.Button
            isDanger
            autofocus
            label={"discard"}
            icon="trash-alt icon-white"
            attributes.form="publishOrDiscardNodes"
            attributes.name="moduleArguments[action]"
            attributes.value="discard"
            attributes.type="submit"
            attributes.hx-target='table'
            attributes.hx-include="#publishOrDiscardNodesForm"
            attributes.hx-swap='outerHTML'
            attributes.hx-on--after-request={'document.getElementById("' + private.popoverId + '").hidePopover()'}
            attributes.hx-post={private.confirmDiscardSelectedChanges}
          />
        </div>
      </section>
    </div>
  `
}
