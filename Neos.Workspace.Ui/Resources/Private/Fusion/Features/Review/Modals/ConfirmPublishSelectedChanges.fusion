prototype(Neos.Workspace.Ui:Component.Modal.ConfirmPublishSelectedChanges) < prototype(Neos.Fusion:Component) {
  baseWorkspaceTitle=''

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'confirm-publish-selected-changes-popover'

    publishSelectedChanges = Neos.Fusion:ActionUri {
      action = 'publishOrDiscardNodes'
      arguments {
        workspace = ${workspaceName}
        action = 'publish'
      }
    }
  }

  renderer = afx`
    <div popover id={private.popoverId} hx-indicator="#indicator">
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.publishSelectedChangesTo').arguments([props.baseWorkspaceTitle])}
        </div>
      </header>
      <section>
        <Neos.Workspace.Ui:Component.Button
          label={"cancel"}
          attributes.popovertarget={private.popoverId}
          attributes.popovertargetaction="close"
        />
        <div class="neos-pull-right">
          <Neos.Workspace.Ui:Component.Button
            isSuccess
            label={"publish"}
            icon="fas fa-check-double icon-white"
            attributes.form="publishOrDiscardNodes"
            attributes.name="moduleArguments[action]"
            attributes.value="publish"
            attributes.type="submit"
            attributes.hx-target='table'
            attributes.hx-include="#publishOrDiscardNodesForm"
            attributes.hx-swap='outerHTML'
            attributes.hx-on--after-request={'document.getElementById("' + private.popoverId + '").hidePopover()'}
            attributes.hx-post={private.publishSelectedChanges}
          />
        </div>
      </section>
    </div>
  `
}
